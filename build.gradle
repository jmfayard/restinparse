apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'

group = 'com.github.jmfayard'

buildscript {
    ext.kotlin_version = "1.1.4-3"

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}


dependencies {

    // Retrofit, Ok, Moshi, Rx
    def retrofit_version = "2.1.0"
    def okhttp_version = "3.4.1"
    compile "com.squareup.retrofit2:retrofit:${retrofit_version}"
    compile "com.squareup.retrofit2:converter-moshi:${retrofit_version}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit_version}"
    compile "com.squareup.okhttp3:okhttp:${okhttp_version}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile 'io.reactivex:rxjava:1.2.2'


    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compile 'joda-time:joda-time:2.9.3'

    /*** Tests **/
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile 'org.testng:testng:6.8.1'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'io.kotlintest:kotlintest:1.3.3'
    testCompile 'com.natpryce:konfig:1.4.1.0'
}
sourceSets {
    test.java.srcDirs += 'src/main/kotlin'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
        }
    }
}

